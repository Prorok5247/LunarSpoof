plugins {
    id 'java'
    id 'org.jetbrains.kotlin.jvm' version '1.5.30'
    id 'net.kyori.blossom' version '1.3.0'
    id 'com.github.johnrengelman.shadow' version '6.1.0'
    id 'net.minecraftforge.gradle.forge' version '86b2392'
    id 'org.spongepowered.mixin' version 'd75e32e'
}

version = "2.0"
group = "dev.isxander"
archivesBaseName = "lunarspoof"

minecraft {
    version = "1.8.9-11.15.1.2318-1.8.9"
    runDir = "run"
    mappings = "stable_22"
}

mixin {
    defaultObfuscationEnv = "searge"
    add sourceSets.main, "mixins.lunarspoof.refmap.json"
}

repositories {
    mavenCentral()

    maven {
        name = 'SpongePowered'
        url = 'https://repo.spongepowered.org/maven/'
    }
    maven {
        name = 'JitPack'
        url = 'https://jitpack.io/'
    }
    maven {
        name = 'Sk1er Public'
        url = 'https://repo.sk1er.club/repository/maven-public/'
    }
}

configurations {
    include
    implementation.extendsFrom(include)
}

dependencies {
    include "gg.essential:loader-launchwrapper:1.1.1"
    implementation "gg.essential:essential-1.8.9-forge:1359"

    implementation "org.java-websocket:Java-WebSocket:1.5.2"

    annotationProcessor "org.spongepowered:mixin:0.7.11-SNAPSHOT"
}

jar {
    archiveName = "[$minecraft.version-$project.version] LunarSpoof (Sources).jar"

    manifest {
        attributes (
                "FMLCorePlugin": "gg.essential.loader.stage0.EssentialSetupTweaker",
                "ModSide": "CLIENT",
                "FMLCorePluginContainsFMLMod": "Yes, yes it does"
        )
    }
}

processResources {
    inputs.property 'mod_id', mod_id
    inputs.property 'mod_version', project.version
    inputs.property 'mod_name', mod_name

    filesMatching('mcmod.info') {
        expand(
                'mod_id': mod_id,
                'mod_version': project.version,
                'mod_name': mod_name
        )
    }
}

// Forge does not import resources properly.
task moveResources {
    doLast {
        ant.move file: "${buildDir}/resources/main",
                todir: "${buildDir}/classes/kotlin"
    }
}

tasks.moveResources.dependsOn processResources
tasks.classes.dependsOn moveResources

shadowJar {
    archiveBaseName.set("[$minecraft.version-$project.version] LunarSpoof")
    archiveVersion.set('')
    archiveClassifier.set('')
    configurations = [project.configurations.include]
    duplicatesStrategy DuplicatesStrategy.EXCLUDE
}

jar.dependsOn shadowJar

blossom {
    def evergreenClass = "src/main/kotlin/dev/isxander/lunarspoof/LunarSpoof.kt"

    replaceToken("__GRADLE_NAME__", mod_name, evergreenClass)
    replaceToken("__GRADLE_ID__", mod_id, evergreenClass)
    replaceToken("__GRADLE_VERSION__", project.version, evergreenClass)
}
