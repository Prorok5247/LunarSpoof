plugins {
    id 'java'
    id 'com.github.johnrengelman.shadow' version '6.1.0'
    id 'net.minecraftforge.gradle.forge' version '86b2392'
    id 'org.spongepowered.mixin' version 'd75e32e'
}

version = "2.0"
group = "co.uk.isxander"
archivesBaseName = "lunarspoof"

minecraft {
    version = "1.8.9-11.15.1.2318-1.8.9"
    runDir = "run"
    mappings = "stable_22"
    makeObfSourceJar = false
}

mixin {
    defaultObfuscationEnv = "notch"
    add sourceSets.main, "mixins.lunarspoof.refmap.json"
}

repositories {
    mavenCentral()
}

configurations {
    include
    implementation.extendsFrom(include)
}

dependencies {
    provided files("libs/modcore.jar")
    provided files("libs/xanderlib.jar")

    include group: 'org.java-websocket', name: 'Java-WebSocket', version: '1.5.2'
    include group: 'org.slf4j', name: 'slf4j-api', version: '1.7.30'
}

jar {
    archiveName = "[$minecraft.version-$project.version] LunarSpoof.jar"

    manifest {
        attributes "FMLCorePlugin": "co.uk.isxander.lunarspoof.forge.FMLLoadingPlugin",
                "ModSide": "CLIENT",
                "FMLCorePluginContainsFMLMod": "Yes, yes it does"
    }
}

processResources {
    inputs.property "version", project.version
    inputs.property "mcversion", project.minecraft.version

    from(sourceSets.main.resources.srcDirs) {
        include 'mcmod.info'

        expand 'version':project.version, 'mcversion':project.minecraft.version
    }

    from(sourceSets.main.resources.srcDirs) {
        exclude 'mcmod.info'
    }
}

// Forge does not import resources properly.
task moveResources {
    doLast {
        ant.move file: "${buildDir}/resources/main",
                todir: "${buildDir}/classes/java"
    }
}

tasks.moveResources.dependsOn processResources
tasks.classes.dependsOn moveResources

shadowJar {
    archiveClassifier.set('')
    configurations = [project.configurations.include]
    duplicatesStrategy DuplicatesStrategy.EXCLUDE
}
